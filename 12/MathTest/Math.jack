/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {

    static Array twoToThe;

    /** Initializes the library. */
    function void init() {
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = -32767;
        let twoToThe[15] = twoToThe[15] - 1;
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if ( x < 0 ) {
            return -x;
        }
        return x;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int ret, sum, i;
        let sum = x;
        let i = 15;
        while (i>-1) {
            if (twoToThe[15-i] & y) {
                let ret = ret + sum;
            }
            let sum = sum + sum;
            let i = i - 1;
        }
        return ret;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var bool change_sign;
        if ( x < 0 ) {
            let x = -x;
            let change_sign = true;
        }
        if ( y < 0 ) {
            let y = -y;
            let change_sign = ~change_sign;
        }
        if ( change_sign ) {
            return -(Math._divide(x, y));
        } else {
            return Math._divide(x, y);
        }
    }

    function int _divide(int x, int y) {
        var int q;
        if ( (y > x) | (y < 0) ) {
            return 0;
        }
        let q = Math._divide(x, 2*y);
        if ( ( x - (2 * q * y) ) < y ) {
            return 2 * q;
        } else {
            return ( 2 * q ) + 1;
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, j, temp, multemp;
        let j = 7;
        while (j > -1) { // while j >= 0
            let temp = y + twoToThe[j];
            let multemp = temp * temp;
            if ( ((multemp-1) < x) & (multemp > 0) ) {
                let y = temp;
            }
            let j = j - 1;
        }
        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if ( a > b ) {
            return a;
        }
        return b;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if ( a < b ) {
            return a;
        }
        return b;
    }
}
